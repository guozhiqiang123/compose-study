plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}


configurations {
    cleanedAnnotations
    api.exclude group: 'org.jetbrains', module: 'annotations'
}

android {
    namespace 'com.gzq.wanandroid'
    compileSdk config['compile_sdk']

    defaultConfig {
        applicationId "com.gzq.wanandroid"
        minSdk config['min_sdk']
        targetSdk config['target_sdk']
        versionCode config['version_code']
        versionName config['version_name']

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        multiDexEnabled true

        //为是room具备自动升级功能，必须设置schemas导出路径
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        release {
            storeFile file('../keystore/wanandroid.keystore')
            storePassword "123456"
            keyAlias "wanandroid"
            keyPassword "123456"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {


    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'


    //网络调试 https://github.com/ChuckerTeam/chucker
    debugImplementation "com.github.chuckerteam.chucker:library:3.5.2"
    releaseImplementation "com.github.chuckerteam.chucker:library-no-op:3.5.2"

//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'

    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.0"))

    //compose
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation 'androidx.compose.material3:material3'
    implementation "androidx.compose.material:material"
    implementation "androidx.compose.runtime:runtime-livedata"
    implementation "androidx.compose.ui:ui-util"
    implementation 'androidx.compose.material3:material3-window-size-class:1.0.0'

    //androidx
    implementation "androidx.core:core-ktx:$core_ktx"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$androidx_lifecycle"
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$androidx_lifecycle"
    implementation "androidx.navigation:navigation-compose:$nav_version"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.webkit:webkit:1.6.1'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidx_lifecycle"

    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"

    //retrofit
    //https://github.com/square/retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.7'
    implementation "com.squareup.okhttp3:logging-interceptor:4.10.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"

    //timber
    //https://github.com/JakeWharton/timber
    implementation "com.jakewharton.timber:timber:5.0.1"

    //下拉刷新
    implementation "com.google.accompanist:accompanist-swiperefresh:0.26.5-rc"

    //glide
    implementation 'com.github.bumptech.glide:glide:4.14.2'
    kapt 'com.github.bumptech.glide:compiler:4.14.2'
    implementation("com.github.bumptech.glide:okhttp-integration:4.14.2") {
        exclude group: "com.squareup.okhttp3", module: "okhttp"
    }

    //coil
    //https://github.com/coil-kt/coil
    implementation "io.coil-kt:coil-compose:2.2.2"

    //sketch
    //https://github.com/panpf/sketch
    implementation "io.github.panpf.sketch3:sketch:3.1.0"
    implementation "io.github.panpf.sketch3:sketch-compose:3.1.0"
    implementation "io.github.panpf.sketch3:sketch-svg:3.1.0"

    //本地存储
    //https://github.com/Tencent/MMKV
    implementation 'com.tencent:mmkv:1.2.14'

    //AgentWeb
    //https://github.com/Justson/AgentWeb
    implementation 'com.github.Justson.AgentWeb:agentweb-core:v5.0.6-androidx'

    //多语言
    //https://github.com/YarikSOffice/lingver
    implementation "com.github.YarikSOffice:lingver:1.3.0"

    //导航动画
    implementation "com.google.accompanist:accompanist-navigation-animation:0.31.2-alpha"

    //权限请求
    implementation "com.google.accompanist:accompanist-permissions:0.31.2-alpha"

    //markdown
    //https://github.com/noties/Markwon
    implementation "io.noties.markwon:core:$markwon"
    implementation "io.noties.markwon:ext-strikethrough:$markwon"
    implementation "io.noties.markwon:ext-tables:$markwon"
    implementation "io.noties.markwon:ext-tasklist:$markwon"
    implementation "io.noties.markwon:image-glide:$markwon"
    implementation "io.noties.markwon:recycler:$markwon"
    implementation "io.noties.markwon:recycler-table:$markwon"
    implementation "io.noties.markwon:html:$markwon"
    implementation "io.noties.markwon:syntax-highlight:$markwon"
    implementation "io.noties:prism4j:$prism4j"
    kapt "io.noties:prism4j-bundler:$prism4j"
    implementation "androidx.cardview:cardview:1.0.0"

    //bugly
    implementation 'com.tencent.bugly:crashreport:4.1.9'

    //room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
}